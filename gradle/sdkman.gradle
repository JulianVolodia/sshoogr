
import groovyx.net.http.HTTPBuilder
import groovyx.net.http.Method
import static groovyx.net.http.Method.*
import static groovyx.net.http.ContentType.*

buildscript {
  repositories { mavenCentral() }
  dependencies {
    classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
  }
}

task releaseOnSdkman << {
  sdkmanRequest('release', POST, [
    candidate: rootProject.name, 
    version: rootProject.sdkmanDefaultVersion, 
    url: "https://repo1.maven.org/maven2/${rootProject.group.split('\\.').join('/')}/${rootProject.name}/${rootProject.sdkmanDefaultVersion}/${rootProject.name}-${rootProject.sdkmanDefaultVersion}.zip"
  ])
}

task setDefaultOnSdkman << {
  sdkmanRequest('default', PUT, [
    candidate: rootProject.name, 
    default: rootProject.sdkmanDefaultVersion
  ])
}

task broadcastOnSdkman << {
  sdkmanRequest('announce/struct', POST, [
    candidate: rootProject.name, 
    version: rootProject.sdkmanDefaultVersion,
    hashtag: rootProject.name
  ])
}

void sdkmanRequest(String operation, Method method, Map jsonBody) {
  sdkman(operation).request(method, JSON) {
    headers.consumer_key = rootProject.sdkmanConsumerKey
    headers.consumer_token = rootProject.sdkmanConsumerToken
    headers.'Content-Type' = 'application/json'
    headers.'Accept' = 'application/json'
    body = jsonBody
    response.success = { resp, json ->
      println "sdkman: ${resp.statusLine}"
      println "sdkman: ${json}"
    }
    response.failure = { resp ->
      println "sdkman: ${resp.status}"
      println "sdkman: ${resp.statusLine}"
      throw new GradleException(resp.statusLine.toString())
    }
  }
}

HTTPBuilder sdkman(String operation) { 
  new HTTPBuilder("https://sdkman-vendor.herokuapp.com/${operation}")
}
                                      